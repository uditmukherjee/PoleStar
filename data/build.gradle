apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
  def config = rootProject.ext.configuration

  compileSdkVersion config.compileSdkVersion
  buildToolsVersion config.buildToolsVersion

  defaultConfig {
    minSdkVersion config.minSdk
    targetSdkVersion config.targetSdk
    versionCode config.versionCode
    versionName config.versionName
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
}

retrolambda {
  jdk System.getenv("JAVA8_HOME") ?: getJavaHome("1.8")
  oldJdk System.getenv("JAVA7_HOME") ?: getJavaHome("1.7")
}

dependencies {
  def testDependencies = rootProject.ext.testDependencies
  def developmentDependencies = rootProject.ext.developmentDependencies
  def supportDependencies = rootProject.ext.supportLibraryDependencies

  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile supportDependencies.annotations

  // testing
  testCompile testDependencies.junit
}

/*
 * Making retrolambda work in any platform.
 */
String getJavaHome(String version) {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine "/usr/libexec/java_home", "-v", version
    standardOutput = stdout;
  }
  return stdout.toString().trim()
}

